/**
 * Build for Fabric3 Gradle plugins
 */
project.description = 'Fabric3 Gradle plugins'

defaultTasks 'clean', 'assemble', 'install'

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

}

def distributions() {
    // define projects that are distributions, i.e. they contain source files; project parent directories will be ignored
    return subprojects.findAll { new File(it.projectDir, "src").exists() }
}

configure(distributions()) {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'org.codehaus.fabric3.gradle'

    dependencies {
        compile gradleApi()

        compile group: 'org.eclipse.aether', name: 'aether-api', version: aetherVersion     {
            exclude group: 'foo'
        }
        compile group: 'org.eclipse.aether', name: 'aether-util', version: aetherVersion
        compile group: 'org.eclipse.aether', name: 'aether-impl', version: aetherVersion
        compile group: 'org.eclipse.aether', name: 'aether-connector-basic', version: aetherVersion
        compile group: 'org.eclipse.aether', name: 'aether-transport-file', version: aetherVersion
        compile group: 'org.eclipse.aether', name: 'aether-transport-http', version: aetherVersion
        compile group: 'org.eclipse.aether', name: 'aether-transport-wagon', version: aetherVersion
        compile group: 'org.apache.maven', name: 'maven-aether-provider', version: '3.1.1'

        testCompile 'junit:junit:4.5'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task docJar(type: Jar, dependsOn: javadoc) {
        classifier = 'docs'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives docJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
//                repository(url: 'file:./maven')
            }
        }
    }

    signing {
        required {
            !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask('uploadArchives')
        }
        sign configurations.archives
    }


}

def plugins() {
    return subprojects.findAll { project -> project.name.startsWith('fabric3-') }
}

configure(plugins()) {
    dependencies {
        compile project(':plugin-core')
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}